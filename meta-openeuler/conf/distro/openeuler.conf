DISTRO = "openeuler"
DISTRO_NAME = "openEuler Embedded(openEuler Embedded Reference Distro)"
DISTRO_VERSION = "23.09"
DISTRO_CODENAME = "openEuler${@d.getVar('DISTRO_VERSION').replace('.', '_')}"
SDK_VENDOR = "-openeulersdk"
SDK_VERSION = "${@d.getVar('DISTRO_VERSION').replace('snapshot-${METADATA_REVISION}', 'snapshot')}"
SDK_VERSION[vardepvalue] = "${SDK_VERSION}"

MAINTAINER = "openEuler <dev@openeuler.org>"

TARGET_VENDOR = "-openeuler"

LOCALCONF_VERSION = "2"

# define default init and device manager
INIT_MANAGER ?= "mdev-busybox"
VIRTUAL-RUNTIME_dev_manager ?= "busybox-mdev"

# Override these in openeuler embedded based distros

# pam package is used for authentication
OPENEULER_DEFAULT_DISTRO_FEATURES = "pam"

DISTRO_FEATURES ?= "${DISTRO_FEATURES_DEFAULT} ${OPENEULER_DEFAULT_DISTRO_FEATURES}"

# Even we uses other init manager (busybox or systemd),
# we need sysvinit DISTRO_FEATURES to enable update-rc.d mechanism
DISTRO_FEATURES += "sysvinit"

# remove currently unsupported features
DISTRO_FEATURES_DEFAULT:remove = "bluetooth x11"

PREFERRED_VERSION_linux-openeuler ?= "5.10%"
PREFERRED_VERSION_linux-openeuler-rt ?= "5.10%"

# setting SOURCE_DATE_EPOCH for reproducible build
SOURCE_DATE_EPOCH = "${@get_openeuler_epoch(d)}"

# SDK related configurations
# set SDK name
SDK_NAME = "${DISTRO}-${TCLIBC}-${SDKMACHINE}-${IMAGE_BASENAME}-${TUNE_PKGARCH}-${MACHINE}"
# set default sdk install directory
SDKPATH = "/opt/${DISTRO}/${SDK_VERSION}"
# The path to default to installing the SDK to
SDKPATHINSTALL = "/opt/${DISTRO}/${SDK_NAME_PREFIX}-${SDK_ARCH}"

TCLIBCAPPEND = ""
# by default glibc is used
TCLIBC ?= "glibc"
# openeuler use external toolchain, see tcmode-external.inc
TCMODE-LIBC ?= "glibc-external"
TCMODE-CRYPT ?= "libxcrypt-external"

# do not use default download dir, use openeuler's configuration
OPENEULER_REPO_NAME ?= "${BPN}"
DL_DIR ?= "${OPENEULER_SP_DIR}/${OPENEULER_REPO_NAME}"

OPENEULER_BRANCH ?= "openEuler-${DISTRO_VERSION}"
OPENEULER_GIT_URL ?= "https://gitee.com/src-openeuler"

BB_SIGNATURE_HANDLER ?= "OEEquivHash"
BB_HASHSERVE ??= "auto"

# set default kernel provider
PREFERRED_PROVIDER_virtual/kernel ?= "linux-openeuler"

# 
PREFERRED_PROVIDER_dhcp-relay = "dhcp"

#UNINATIVE_CHECKSUM[x86_64] = "dead"
#UNINATIVE_URL = "unset"

INHIBIT_PACKAGE_DEBUG_SPLIT = "1"

# for all .bb file, OPENEULER_SP_DIR  will be included
# in file path
FILESEXTRAPATHS:prepend = "${OPENEULER_SP_DIR}:"

INHERIT += "openeuler"

# disable the file checksum of fetch
do_fetch[file-checksums] = ""
SDK_INCLUDE_BUILDTOOLS = "0"
require conf/distro/openeuler-bsp.conf

# OPENEULER_SP_DIR should never be included when generating the signatures
BB_BASEHASH_IGNORE_VARS:append = " OPENEULER_SP_DIR"

# define root home, default /home/root
ROOT_HOME ?= "/root"


# whether to use openeuler_hosttools.inc
OPENEULER_HOSTTOOLS_INC = "${@['', 'conf/distro/include/openeuler_hosttools.inc']['${OPENEULER_PREBUILT_TOOLS_ENABLE}' == 'yes']}"

require ${OPENEULER_HOSTTOOLS_INC}

# used to unpack source such as *.tar.xz, *.rpm, etc
HOSTTOOLS += "xz xzcat"
