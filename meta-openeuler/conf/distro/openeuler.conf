DISTRO = "openeuler"
DISTRO_NAME = "openEuler Embedded(openEuler Embedded Reference Distro)"
DISTRO_VERSION = "23.03"
DISTRO_CODENAME = "openEuler${@d.getVar('DISTRO_VERSION').replace('.', '_')}"
SDK_VENDOR = "-openeulersdk"
SDK_VERSION = "${@d.getVar('DISTRO_VERSION').replace('snapshot-${METADATA_REVISION}', 'snapshot')}"
SDK_VERSION[vardepvalue] = "${SDK_VERSION}"

MAINTAINER = "openEuler <dev@openeuler.org>"

TARGET_VENDOR = "-openeuler"

LOCALCONF_VERSION = "2"

# define default init and device manager
INIT_MANAGER ?= "mdev-busybox"
VIRTUAL-RUNTIME_dev_manager ?= "busybox-mdev"

# Override these in openeuler embedded based distros

# pam package is used for authentication
OPENEULER_DEFAULT_DISTRO_FEATURES = "pam"

DISTRO_FEATURES ?= "${DISTRO_FEATURES_DEFAULT} ${OPENEULER_DEFAULT_DISTRO_FEATURES}"

# Even we uses other init manager (busybox or systemd),
# we need sysvinit DISTRO_FEATURES to enable update-rc.d mechanism
DISTRO_FEATURES += "sysvinit"

# remove currently unsupported features
DISTRO_FEATURES_DEFAULT_remove = "bluetooth x11"

PREFERRED_VERSION_linux-openeuler ?= "5.10%"
PREFERRED_VERSION_linux-openeuler-rt ?= "5.10%"

# SDK related configurations
# set SDK name
SDK_NAME = "${DISTRO}-${TCLIBC}-${SDKMACHINE}-${IMAGE_BASENAME}-${TUNE_PKGARCH}-${MACHINE}"
# set default sdk install directory
SDKPATH = "/opt/${DISTRO}/${SDK_VERSION}"
# The path to default to installing the SDK to
SDKPATHINSTALL = "/opt/${DISTRO}/${SDK_NAME_PREFIX}-${SDK_ARCH}"

# not supported now
DISTRO_EXTRA_RDEPENDS += "${OPENEULER_DEFAULT_EXTRA_RDEPENDS}"
DISTRO_EXTRA_RRECOMMENDS += "${OPENEULER_DEFAULT_EXTRA_RRECOMMENDS}"

TCLIBCAPPEND = ""
# by default glibc is used
TCLIBC ?= "glibc"
# openeuler use external toolchain, see tcmode-external.inc
TCMODE-LIBC ?= "glibc-external"
TCMODE-CRYPT ?= "libxcrypt-external"

# do not use default download dir, use openeuler's configuration
OPENEULER_REPO_NAME ?= "${BPN}"
DL_DIR ?= "${OPENEULER_SP_DIR}/${OPENEULER_REPO_NAME}"

OPENEULER_BRANCH ?= "openEuler-${DISTRO_VERSION}"
OPENEULER_GIT_URL ?= "https://gitee.com/src-openeuler"

INHERIT += "reproducible_build"

BB_SIGNATURE_HANDLER ?= "OEEquivHash"
BB_HASHSERVE ??= "auto"

# set default kernel provider
PREFERRED_PROVIDER_virtual/kernel ?= "linux-openeuler"

#UNINATIVE_CHECKSUM[x86_64] = "dead"
#UNINATIVE_URL = "unset"

INHIBIT_PACKAGE_DEBUG_SPLIT = "1"

# for all .bb file, OPENEULER_SP_DIR  will be included
# in file path
FILESEXTRAPATHS_prepend = "${OPENEULER_SP_DIR}:"

# the sysroot of openeuler native sdk
OPENEULER_NATIVESDK_SYSROOT ?= ""


# PACKAGE_NO_LOCALE is variable in package.bbclass to control
# whether do_split_locales() for packages.
# openEuler Embedded is used for scenarios with limit memory, also
# in current image, no locale related packages are included.
# so we use variable to disable the generation of locale related packages
# it will save some build time, also no extra locale-* packages
PACKAGE_NO_LOCALE ?= "1"

INHERIT += "openeuler"

# disable the file checksum of fetch
do_fetch[file-checksums] = ""
SDK_INCLUDE_BUILDTOOLS = "0"
require conf/distro/openeuler-bsp.conf

# OPENEULER_SP_DIR should never be included when generating the signatures
BB_HASHBASE_WHITELIST_append = " OPENEULER_SP_DIR"

# define root home, default /home/root
ROOT_HOME ?= "/root"
